#!/usr/bin/perl
use v5.30;
use feature qw(signatures);
use warnings;
no warnings qw(experimental::signatures);
use Encode;
use Getopt::Long qw(:config posix_default no_ignore_case bundling auto_help);
use HTTP::Tiny;
use IO::Select;
use IO::Socket;
use JSON;
use List::Util qw(reduce);
use Path::Tiny;
use Time::Piece;
use URI;
use URI::QueryParam;

use constant +{
    VERSION_STRING => 'gimp/0.0.1',
    CLIENT_END => 0,
    CLIENT_REQUEST => 1,
    CLIENT_VERSION => 2,
    CLIENT_HOST => 3,
    SERVER_ERROR => 0,
    SERVER_FOUND => 1,
    SERVER_NOT_FOUND => 4,
    BUF_SIZE => 512,
    TIMEOUT => 10,
    CACHE_EXPIRATION => 24 * 60 * 60,  # 1 day
};

binmode STDOUT, ':utf8';
binmode STDERR, ':utf8';

sub log_info :prototype($@);
sub log_warn :prototype($@);

sub process($host, $port, $cache_path) {
    my $listner = IO::Socket::INET->new(
        Proto => 'tcp',
        LocalAddr => $host,
        LocalPort => $port,
        Listen => SOMAXCONN,
        Reuse => 1,
    ) or die "can't listen: $!";

    my $selector = IO::Select->new($listner);

    while (my @ready = $selector->can_read) {
        for my $socket (@ready) {
            if ($socket == $listner) {
                my $new_socket = $listner->accept;
                $selector->add($new_socket);
            } else {
                while ($socket->sysread(my $buf, BUF_SIZE)) {
                    my $command = substr $buf, 0, 1;
                    log_info 'command: %d', $command;
                    if ($command == CLIENT_END) {
                        break;
                    } elsif ($command == CLIENT_REQUEST) {
                        my $command_end = (index $buf, ' ', 1) || (index $buf, "\n", 1);
                        my $kana = substr $buf, 1, $command_end - 1;
                        my $decoded = decode(eucjp => $kana);
                        log_info 'kana: %s', $decoded;
                        my $result = eval {
                            if (my $kanji = search($decoded, $cache_path)) {
                                sprintf "%d/%s\n", SERVER_FOUND, $kanji;
                            } else {
                                sprintf "%d%s \n", SERVER_NOT_FOUND, $decoded;
                            }
                        };
                        if ($@) {
                            log_warn 'error in process: %s', $@;
                            $result = sprintf "%d\n", SERVER_ERROR;
                        }
                        log_info 'result: %s', $result;
                        $socket->write(encode(eucjp => $result));
                    } elsif ($command == CLIENT_VERSION) {
                        $socket->write(VERSION_STRING . ' ');
                    } elsif ($command == CLIENT_HOST) {
                        my $result = sprintf '%s:%s: ', $socket->sockhost, $socket->sockport;
                        $socket->write($result);
                    }
                    $selector->remove($socket);
                    $socket->close;
                }
            }
        }
    }
}

our $CACHE;
sub search($kana, $cache_path, $now = time) {
    $CACHE //= load_cache($cache_path);
    if ($CACHE->{$kana} && $now < $CACHE->{$kana}{ctime} + CACHE_EXPIRATION) {
        log_info 'cache hit: %s', $kana;
        $CACHE->{$kana}{kanji};
    } else {
        my $kanji = google_ime($kana);
        if (defined $kanji) {
            $CACHE->{$kana} = {
                ctime => $now,
                kanji => $kanji,
            };
        }
        save_cache($cache_path);
        $kanji;
    }
}

sub load_cache($cache_path) {
    if (!$cache_path->exists) {
        return +{};
    }
    my @lines = $cache_path->lines_utf8;
    if (@lines == 0) {
        return +{};
    }
    $CACHE = reduce {
        my ($ctime, $kana, $kanji) = split /\t/, $b;
        $a->{$kana} = +{ ctime => $ctime, kanji => $kanji };
        $a;
    } +{}, $cache_path->lines_utf8;
}

sub save_cache($cache_path) {
    $cache_path->spew_utf8(
        map {
            sprintf "%d\t%s\t%s\n", $CACHE->{$_}{ctime}, $_, $CACHE->{$_}{kanji}
        } sort keys $CACHE->%*
    );
}

sub make_uri($kana) {
    (my $text = $kana) =~ s/([a-z]?)$/,$1/;
    my $uri = URI->new('http://www.google.com/transliterate');
    $uri->query_param('langpair', 'ja-Hira|ja');
    $uri->query_param('text', $text);
    $uri;
}

sub google_ime($kana) {
    my $result = eval {
        my $response = HTTP::Tiny->new(timeout => TIMEOUT)->get(make_uri($kana));
        if ($response->{success}) {
            my $decoded = decode_json($response->{content});
            join '/', $decoded->[0][1]->@*;
        } else {
            undef;
        }
    };
    if ($@) {
        log_warn 'error in google_ime: %s', $@;
    }
    $result;
}

our $LOG_LEVEL = 0;
sub main {
    my %opt = (
        host => '127.0.0.1',
        port => 55100,
        cache => "$ENV{HOME}/.cache/gisp.json",
        help => undef,
        verbose => undef,
    );
    GetOptions(
        \%opt, qw(
            host|h=s
            port|p=i
            cache|c=s
            help|h
            verbose|v
        ),
    ) or pod2usage(1);
    $LOG_LEVEL = 1 if $opt{verbose};
    pod2usage(-exitval => 0, -verbose => 2) if $opt{help};
    process($opt{host}, $opt{port}, path($opt{cache}))
}

sub log_info :prototype($@) ($fmt, @args) {
    if ($LOG_LEVEL > 0) {
        printf "[gisp] %s $fmt\n", localtime->strftime('%FT%T'), @args;
    }
}

sub log_warn :prototype($@) ($fmt, @args) {
    warn sprintf "[gisp] %s $fmt\n", localtime->strftime('%FT%T'), @args;
}

main if $0 eq __FILE__;

1;
